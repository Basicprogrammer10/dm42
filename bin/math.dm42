// Various math functions missing from the dm42

// - LCM
// - GCF
// - Log_y(x)
// - xth root of y
// - Hypot
// - Quadratic Solver

export def math {
    CLMENU
    "lcm",    KEY 1 XEQ lcm
    "gcf",    KEY 2 XEQ gcf
    "logy x", KEY 3 XEQ log_yx
    "x√y",    KEY 4 XEQ x_root_y
    "hypot",  KEY 5 XEQ hypot
    "quad",   KEY 6 XEQ quadratic
    if { FS? 03 } {
        KEY 9 GTO custom_menu
    }
    MENU
    STOP
}

def custom_menu { SF 27 }

// == Functions ==

def gcf {
    do while { X!=0? } {
        MOD
        LASTX
        X<>Y
    }
    DROP
    ABS
}

def lcm {
    DUPN 2
    gcf()
    /
    *
}

def log_yx {
    LOG
    X<>Y
    LOG
    /
}

def x_root_y {
    1/X
    Y^X
}

def hypot {
    X↑2
    X<>Y
    X↑2
    +
    SQRT   
}

def quadratic {
    RCL ST Z
    +/-
    STO/ ST Z
    /
    X<>Y
    2
    /
    STO ST Z
    STO ST T
    X↑2
    +
    SQRT
    STO- ST Z
    +
}